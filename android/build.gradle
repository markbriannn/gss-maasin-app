// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "34.0.0"
        minSdkVersion = 23
        compileSdkVersion = 34
        targetSdkVersion = 34
        ndkVersion = "25.1.8937393"
        kotlinVersion = "1.9.22"
        
        // Play Services versions - use consistent versions across all modules
        playServicesLocationVersion = "21.0.1"
        playServicesMapsVersion = "18.2.0"
        playServicesAuthVersion = "21.0.0"
        playServicesBaseVersion = "18.3.0"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.6.1")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
        classpath("com.google.gms:google-services:4.4.4")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
}

// Force consistent play-services versions across ALL modules
subprojects {
    afterEvaluate { project ->
        project.configurations.all {
            resolutionStrategy.eachDependency { details ->
                if (details.requested.group == 'com.google.android.gms') {
                    if (details.requested.name == 'play-services-location') {
                        details.useVersion rootProject.ext.playServicesLocationVersion
                    }
                    if (details.requested.name == 'play-services-maps') {
                        details.useVersion rootProject.ext.playServicesMapsVersion
                    }
                    if (details.requested.name == 'play-services-auth') {
                        details.useVersion rootProject.ext.playServicesAuthVersion
                    }
                    if (details.requested.name == 'play-services-base') {
                        details.useVersion rootProject.ext.playServicesBaseVersion
                    }
                }
            }
        }
    }
}
